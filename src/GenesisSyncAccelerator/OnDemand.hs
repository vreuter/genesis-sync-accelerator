{-# LANGUAGE BlockArguments #-}
{-# LANGUAGE DeriveAnyClass #-}
{-# LANGUAGE DeriveGeneric #-}
{-# LANGUAGE FlexibleContexts #-}
{-# LANGUAGE LambdaCase #-}
{-# LANGUAGE MonoLocalBinds #-}
{-# LANGUAGE NamedFieldPuns #-}
{-# LANGUAGE PackageImports #-}
{-# LANGUAGE RankNTypes #-}
{-# LANGUAGE ScopedTypeVariables #-}
{-# LANGUAGE StandaloneDeriving #-}
{-# LANGUAGE TupleSections #-}
{-# LANGUAGE TypeApplications #-}
{-# LANGUAGE UndecidableInstances #-}

-- | On-demand fetching for ImmutableDB chunk files.
--
-- This module provides streaming helpers that download missing chunks from a CDN
-- and serve them using local iterators that read directly from downloaded files.
module GenesisSyncAccelerator.OnDemand
  ( OnDemandConfig (..)
  , OnDemandRuntime (..)
  , OnDemandTip (..)
  , OnDemandState (..)
  , deleteChunkFiles
  , ensureChunks
  , newOnDemandRuntime
  , onDemandIteratorForRange
  , onDemandIteratorFrom
  , readOnDemandTip
  , tipFromRemote
  ) where

import Control.Monad (forM, unless, void)
import Control.Monad.IO.Class (MonadIO, liftIO)
import Data.ByteString.Lazy (ByteString)
import qualified Data.ByteString.Lazy as LBS
import Data.List (delete)
import Data.Proxy (Proxy (..))
import Data.Set (Set)
import qualified Data.Set as Set
import GHC.Generics (Generic)
import qualified GenesisSyncAccelerator.RemoteStorage as Remote
import GenesisSyncAccelerator.Tracing (TraceRemoteStorageEvent (..))
import Ouroboros.Consensus.Block
  ( BlockNo (..)
  , CodecConfig
  , ConvertRawHash (..)
  , HasHeader
  , Header
  , HeaderHash
  , IsEBB (..)
  , NestedCtxt
  , Point (BlockPoint, GenesisPoint)
  , RealPoint (..)
  , SlotNo (..)
  , StandardHash
  , WithOrigin (..)
  , pointSlot
  , realPointSlot
  )
import Ouroboros.Consensus.Storage.Common
  ( BlockComponent (GetHeader)
  , StreamFrom (..)
  , StreamTo (..)
  )
import Ouroboros.Consensus.Storage.ImmutableDB.API
  ( Iterator (..)
  , IteratorResult (..)
  )
import Ouroboros.Consensus.Storage.ImmutableDB.Chunks.Internal (ChunkInfo, ChunkNo (..))
import qualified Ouroboros.Consensus.Storage.ImmutableDB.Chunks.Layout as ChunkLayout
import qualified Ouroboros.Consensus.Storage.ImmutableDB.Impl.Index.Primary as Primary
import Ouroboros.Consensus.Storage.ImmutableDB.Impl.Index.Secondary (Entry (..))
import qualified Ouroboros.Consensus.Storage.ImmutableDB.Impl.Index.Secondary as Secondary
import Ouroboros.Consensus.Storage.ImmutableDB.Impl.Iterator (extractBlockComponent)
import Ouroboros.Consensus.Storage.ImmutableDB.Impl.Types (WithBlockSize (..))
import Ouroboros.Consensus.Storage.ImmutableDB.Impl.Util
  ( fsPathChunkFile
  , fsPathPrimaryIndexFile
  , fsPathSecondaryIndexFile
  )
import Ouroboros.Consensus.Storage.Serialisation
import Ouroboros.Consensus.Util.IOLike
  ( IOLike
  , NoThunks
  , STM
  , SomeException
  , StrictTVar
  , atomically
  , newTVarIO
  , readTVar
  , readTVarIO
  , try
  )
import Ouroboros.Consensus.Util.NormalForm.StrictTVar (writeTVar)
import Ouroboros.Network.Block (blockHash, blockNo, blockSlot)
import System.FS.API (HasFS, OpenMode (ReadMode), hGetSize, removeFile, withFile)
import "contra-tracer" Control.Tracer (showTracing, stdoutTracer, traceWith)

-- | Configuration for on-demand fetching.
data OnDemandConfig m blk h = OnDemandConfig
  { odcRemote :: Remote.RemoteStorageConfig
  -- ^ CDN connection details.
  , odcTracer :: Remote.RemoteStorageTracer IO
  -- ^ Tracer for remote storage events.
  , odcChunkInfo :: ChunkInfo
  -- ^ Information about chunk sizes used for slot-to-chunk translation.
  , odcHasFS :: HasFS m h
  -- ^ File system handle for saving downloaded chunks.
  , odcCodecConfig :: CodecConfig blk
  -- ^ Codec configuration for block extraction.
  , odcCheckIntegrity :: blk -> Bool
  -- ^ Integrity check for extracted blocks.
  , odcMaxCachedChunks :: Int
  -- ^ Maximum number of chunks to keep in cache.
  }

data OnDemandRuntime m blk h = OnDemandRuntime
  { odrConfig :: OnDemandConfig m blk h
  , odrState :: StrictTVar m (OnDemandState blk)
  }

-- | Initializes the on-demand runtime by fetching the current tip from the remote storage.
newOnDemandRuntime ::
  forall blk m h.
  (IOLike m, MonadIO m, StandardHash blk, ConvertRawHash blk) =>
  OnDemandConfig m blk h ->
  m (OnDemandRuntime m blk h)
newOnDemandRuntime cfg = do
  tip <- liftIO $ Remote.fetchTipInfo (odcTracer cfg) (odcRemote cfg) >>= procTip . fmap tipFromRemote
  stateVar <- newTVarIO (OnDemandState Set.empty [] tip)
  pure $ OnDemandRuntime cfg stateVar
 where
  procTip = either (\e -> traceWith (odcTracer cfg) (TraceDownloadFailure e) >> pure Nothing) (pure . Just)

-- | Internal state tracking which chunks have been downloaded during the current session.
data OnDemandState blk = OnDemandState
  { odsCachedChunks :: Set ChunkNo
  -- ^ Set of chunk indices already present on disk.
  , odsUsageOrder :: [ChunkNo]
  -- ^ Ordered list of chunks in cache, from Most Recently Used to Least Recently Used.
  , odsTip :: Maybe (OnDemandTip blk)
  -- ^ Tip from on-demand data or remote metadata.
  }
  deriving Generic

data OnDemandTip blk = OnDemandTip
  { odtSlot :: SlotNo
  , odtHash :: HeaderHash blk
  , odtBlockNo :: BlockNo
  }
  deriving Generic

deriving instance Eq (HeaderHash blk) => Eq (OnDemandTip blk)

deriving instance Show (HeaderHash blk) => Show (OnDemandTip blk)

deriving instance NoThunks (HeaderHash blk) => NoThunks (OnDemandTip blk)

deriving instance NoThunks (HeaderHash blk) => NoThunks (OnDemandState blk)

onDemandIteratorForRange ::
  forall m blk h b.
  ( IOLike m
  , MonadIO m
  , HasHeader blk
  , HasHeader (Header blk)
  , DecodeDisk blk (ByteString -> blk)
  , DecodeDiskDep (NestedCtxt Header) blk
  , ReconstructNestedCtxt Header blk
  , ConvertRawHash blk
  ) =>
  OnDemandRuntime m blk h ->
  BlockComponent blk b ->
  StreamFrom blk ->
  StreamTo blk ->
  m (Iterator m blk b)
onDemandIteratorForRange OnDemandRuntime{odrConfig = cfg@OnDemandConfig{odcChunkInfo}, odrState} component from to =
  mkOnDemandIterator cfg odrState component from (Just to) (getChunksInRange odcChunkInfo from to)

onDemandIteratorFrom ::
  forall m blk h b.
  ( IOLike m
  , MonadIO m
  , HasHeader blk
  , HasHeader (Header blk)
  , DecodeDisk blk (ByteString -> blk)
  , DecodeDiskDep (NestedCtxt Header) blk
  , ReconstructNestedCtxt Header blk
  , ConvertRawHash blk
  ) =>
  OnDemandRuntime m blk h ->
  BlockComponent blk b ->
  StreamFrom blk ->
  m (Iterator m blk b)
onDemandIteratorFrom OnDemandRuntime{odrConfig = cfg@OnDemandConfig{odcChunkInfo}, odrState} component from =
  mkOnDemandIterator cfg odrState component from Nothing (chunksFrom odcChunkInfo from)

readOnDemandTip :: IOLike m => OnDemandRuntime m blk h -> STM m (Maybe (OnDemandTip blk))
readOnDemandTip OnDemandRuntime{odrState} = odsTip <$> readTVar odrState

-- | Creates an iterator that downloads and serves chunks one by one.
mkOnDemandIterator ::
  forall m blk h b.
  ( IOLike m
  , MonadIO m
  , HasHeader blk
  , HasHeader (Header blk)
  , DecodeDisk blk (ByteString -> blk)
  , DecodeDiskDep (NestedCtxt Header) blk
  , ReconstructNestedCtxt Header blk
  , ConvertRawHash blk
  ) =>
  OnDemandConfig m blk h ->
  StrictTVar m (OnDemandState blk) ->
  BlockComponent blk b ->
  StreamFrom blk ->
  Maybe (StreamTo blk) ->
  [ChunkNo] ->
  m (Iterator m blk b)
mkOnDemandIterator cfg@OnDemandConfig{odcHasFS, odcChunkInfo, odcCodecConfig, odcCheckIntegrity} stateVar component from to chunks = do
  varChunks <- newTVarIO chunks
  varCurrentIt <- newTVarIO Nothing

  let
    next = do
      current <- readTVarIO varCurrentIt
      case current of
        Just it -> do
          res <- iteratorNext it
          case res of
            IteratorResult b -> return (IteratorResult b)
            IteratorExhausted -> do
              iteratorClose it
              atomically $ writeTVar varCurrentIt Nothing
              next -- Transition to next chunk
        Nothing -> do
          cs <- readTVarIO varChunks
          case cs of
            [] -> return IteratorExhausted
            (c : rest) -> do
              atomically $ writeTVar varChunks rest
              -- Download only the current chunk before serving it
              ok <- ensureChunks cfg stateVar [c]
              if not ok
                then return IteratorExhausted
                else do
                  it <-
                    mkRawChunkIterator
                      odcHasFS
                      odcChunkInfo
                      odcCodecConfig
                      odcCheckIntegrity
                      component
                      from
                      to
                      [c]
                      stateVar
                  atomically $ writeTVar varCurrentIt (Just it)
                  next -- Transition to next chunk
    hasNext =
      readTVar varCurrentIt >>= \case
        Just it -> iteratorHasNext it
        Nothing -> return Nothing

    close =
      readTVarIO varCurrentIt >>= \case
        Just it -> iteratorClose it
        Nothing -> return ()

  return Iterator{iteratorNext = next, iteratorHasNext = hasNext, iteratorClose = close}

-- | Ensures that the requested chunks are present on disk, downloading them if necessary.
-- Implements an LRU eviction policy to maintain the 'odcMaxCachedChunks' limit.
ensureChunks ::
  forall m blk h.
  (IOLike m, MonadIO m) =>
  OnDemandConfig m blk h ->
  StrictTVar m (OnDemandState blk) ->
  [ChunkNo] ->
  m Bool
ensureChunks OnDemandConfig{odcRemote, odcTracer, odcHasFS, odcMaxCachedChunks} stateVar requestedChunks = do
  -- 1. Identify and download missing chunks
  state <- readTVarIO stateVar
  let missingChunks = filter (`Set.notMember` odsCachedChunks state) requestedChunks

  downloadResult <- liftIO $ mapM (Remote.downloadChunk odcTracer odcRemote) missingChunks

  case sequence_ downloadResult of
    Left _ -> return False
    Right _ -> do
      -- 2. Update usage order and identify chunks to prune
      toPrune <- atomically $ do
        curr <- readTVar stateVar
        let
          -- Move requested chunks to the head (most recently used)
          newUsage = requestedChunks ++ foldr delete (odsUsageOrder curr) requestedChunks
          newCached = Set.union (odsCachedChunks curr) (Set.fromList requestedChunks)

          -- Eviction: if we exceed the limit, prune the least recently used chunks.
          -- We must keep all chunks currently requested to ensure iterator safety.
          (stay, prune) = splitAt (max odcMaxCachedChunks (length requestedChunks)) newUsage
          updatedCached = Set.difference newCached (Set.fromList prune)

        writeTVar stateVar curr{odsCachedChunks = updatedCached, odsUsageOrder = stay}
        return prune

      -- 3. Physically delete pruned chunks from disk
      unless (null toPrune) $
        mapM_ (deleteChunkFiles odcHasFS) toPrune

      return True

-- | Deletes the triad of files associated with a chunk.
deleteChunkFiles :: IOLike m => HasFS m h -> ChunkNo -> m ()
deleteChunkFiles hasFS chunk = do
  hRemove hasFS (fsPathChunkFile chunk)
  hRemove hasFS (fsPathPrimaryIndexFile chunk)
  hRemove hasFS (fsPathSecondaryIndexFile chunk)
 where
  hRemove h f = void $ try @_ @SomeException $ removeFile h f

-- | Identifies the set of chunks covering a given streaming range.
getChunksInRange :: ChunkInfo -> StreamFrom blk -> StreamTo blk -> [ChunkNo]
getChunksInRange chunkInfo from to =
  let startChunk = chunkForFrom chunkInfo from
      endChunk = chunkForTo chunkInfo to
   in chunksBetween startChunk endChunk
 where
  -- TODO: chunksBetween from ouroborous-consensus is incorrect, override locally to avoid this issue.
  -- Remove this function when the fix is merged upstream.
  -- See: https://github.com/tweag/genesis-sync-accelerator/issues/7
  chunksBetween :: ChunkNo -> ChunkNo -> [ChunkNo]
  chunksBetween (ChunkNo a) (ChunkNo b) = map ChunkNo $ if b < a then [b .. a] else [a .. b]

-- | Translates a 'StreamFrom' bound to its starting 'ChunkNo'.
chunkForFrom :: ChunkInfo -> StreamFrom blk -> ChunkNo
chunkForFrom ci (StreamFromInclusive pt) = ChunkLayout.chunkIndexOfSlot ci (realPointSlot pt)
chunkForFrom ci (StreamFromExclusive pt) = case pointSlot pt of
  Origin -> ChunkNo 0
  NotOrigin slot -> ChunkLayout.chunkIndexOfSlot ci slot

-- | Translates a 'StreamTo' bound to its ending 'ChunkNo'.
chunkForTo :: ChunkInfo -> StreamTo blk -> ChunkNo
chunkForTo ci (StreamToInclusive pt) = ChunkLayout.chunkIndexOfSlot ci (realPointSlot pt)

-- | Creates a "Raw Chunk Iterator" that serves blocks from a specific list of chunks.
--
-- This iterator is "stateless" in the sense that it does not rely on the global
-- 'ImmutableDB' state (tip, indices, etc.). Instead, it directly parses the
-- secondary index files on disk to find the requested blocks.
mkRawChunkIterator ::
  forall m blk b h.
  ( IOLike m
  , MonadIO m
  , HasHeader blk
  , HasHeader (Header blk)
  , DecodeDisk blk (LBS.ByteString -> blk)
  , DecodeDiskDep (NestedCtxt Header) blk
  , ReconstructNestedCtxt Header blk
  , ConvertRawHash blk
  ) =>
  HasFS m h ->
  ChunkInfo ->
  CodecConfig blk ->
  -- | Integrity check function to validate blocks read from disk.
  (blk -> Bool) ->
  -- | The component of the block to stream (e.g., the whole block, just the header, etc.).
  BlockComponent blk b ->
  -- | Stream lower bound: entries at or after this point are streamed.
  -- Note that inclusivity depends on the constructor being used (StreamFromInclusive vs StreamFromExclusive).
  StreamFrom blk ->
  -- | Stream upper bound (always inclusive): entries up to and including this point are streamed.
  Maybe (StreamTo blk) ->
  -- | The list of chunks (epochs) to iterate over.
  [ChunkNo] ->
  StrictTVar m (OnDemandState blk) ->
  m (Iterator m blk b)
mkRawChunkIterator hasFS chunkInfo codecConfig checkIntegrity component from to chunks stateVar = do
  -- 1. Read all entries from all requested chunks.
  -- We map over the chunks, open the corresponding secondary index file, and parse all entries.
  allEntries <- forM chunks $ \chunk -> do
    chunkSize <- withFile hasFS (fsPathChunkFile chunk) ReadMode (hGetSize hasFS)
    -- Determine per-chunk whether the first entry is an EBB by reading
    -- the primary index, rather than assuming all chunks start with EBBs.
    mbFirstSlot <- Primary.readFirstFilledSlot (Proxy @blk) hasFS chunkInfo chunk
    let firstIsEBB = maybe IsNotEBB ChunkLayout.relativeSlotIsEBB mbFirstSlot
    entries <- Secondary.readAllEntries hasFS 0 chunk (const False) chunkSize firstIsEBB
    return $ map (chunk,) entries

  let flatEntries =
        maybe id (applyStreamTo chunkInfo) to
          . applyStreamFrom chunkInfo from
          $ concat allEntries
  varEntries <- newTVarIO flatEntries

  -- 2. Define the 'iteratorNext' action.
  -- This action pops the next entry from the queue, opens the corresponding chunk file,
  -- reads the block data, and extracts the requested component.
  let next =
        readTVarIO varEntries >>= \case
          [] -> return IteratorExhausted
          ((chunk, WithBlockSize size entry) : rest) -> do
            traceWith stdoutTracer $
              "Serving block at " ++ show (tipToRealPoint chunkInfo entry) ++ " from chunk " ++ show chunk
            atomically $ writeTVar varEntries rest
            -- We open the file for every block. This is inefficient but safe.
            -- Optimization: Keep the file handle open until the chunk changes.
            res <- withFile hasFS (fsPathChunkFile chunk) ReadMode $ \hnd ->
              extractBlockComponent
                hasFS
                chunkInfo
                chunk
                codecConfig
                checkIntegrity
                hnd
                (WithBlockSize size entry)
                component
            header <- withFile hasFS (fsPathChunkFile chunk) ReadMode $ \hnd ->
              extractBlockComponent
                hasFS
                chunkInfo
                chunk
                codecConfig
                checkIntegrity
                hnd
                (WithBlockSize size entry)
                GetHeader
            let newTip = OnDemandTip (blockSlot header) (blockHash header) (blockNo header)
            traceWith (showTracing stdoutTracer) $ "Updating on-demand tip to " ++ show newTip
            atomically $ do
              curr <- readTVar stateVar
              writeTVar stateVar curr{odsTip = Just newTip}
            return $ IteratorResult res

      -- 3. Define the 'iteratorHasNext' action.
      -- Peeks at the next entry in the queue to return its Point.
      hasNext =
        readTVar varEntries >>= \case
          [] -> return Nothing
          ((_, WithBlockSize _ entry) : _) ->
            return $ Just (tipToRealPoint chunkInfo entry)

      -- 4. Define the 'iteratorClose' action.
      -- Since we don't keep persistent file handles (we open/close per block),
      -- there is nothing to clean up here.
      close = return ()

  return Iterator{iteratorNext = next, iteratorHasNext = hasNext, iteratorClose = close}

-- | Helper to convert an Index 'Entry' (which stores hash and slot/epoch)
-- into a 'RealPoint' (which uses SlotNo).
tipToRealPoint :: ChunkInfo -> Entry blk -> RealPoint blk
tipToRealPoint ci Secondary.Entry{blockOrEBB, headerHash} =
  RealPoint (ChunkLayout.slotNoOfBlockOrEBB ci blockOrEBB) headerHash

chunksFrom :: ChunkInfo -> StreamFrom blk -> [ChunkNo]
chunksFrom ci from = iterate nextChunk (chunkForFrom ci from)
 where
  nextChunk (ChunkNo n) = ChunkNo (n + 1)

-- | Filter entries to honour the 'StreamFrom' lower bound.
--
-- For 'StreamFromExclusive pt', drops entries at or before @pt@.
-- For 'StreamFromInclusive pt', drops entries strictly before @pt@.
-- Entries within a chunk are ordered (EBB first, then regular blocks by slot),
-- so a simple 'dropWhile' from the front suffices.
applyStreamFrom ::
  Eq (HeaderHash blk) =>
  ChunkInfo ->
  StreamFrom blk ->
  [(ChunkNo, WithBlockSize (Entry blk))] ->
  [(ChunkNo, WithBlockSize (Entry blk))]
applyStreamFrom ci = \case
  StreamFromExclusive GenesisPoint -> id
  StreamFromExclusive (BlockPoint fromSlot fromHash) ->
    dropWhile $ \(_, WithBlockSize _ e) ->
      let eSlot = ChunkLayout.slotNoOfBlockOrEBB ci (blockOrEBB e)
       in eSlot < fromSlot || (eSlot == fromSlot && headerHash e == fromHash)
  StreamFromInclusive (RealPoint fromSlot fromHash) ->
    dropWhile $ \(_, WithBlockSize _ e) ->
      let eSlot = ChunkLayout.slotNoOfBlockOrEBB ci (blockOrEBB e)
       in eSlot < fromSlot || (eSlot == fromSlot && headerHash e /= fromHash)

-- | Filter entries to honour the 'StreamTo' upper bound.
--
-- For 'StreamToInclusive pt', takes entries up to and including @pt@.
-- Entries within a chunk are ordered (EBB first, then regular blocks by slot),
-- so a simple 'takeWhile' from the front suffices.
applyStreamTo ::
  ChunkInfo ->
  StreamTo blk ->
  [(ChunkNo, WithBlockSize (Entry blk))] ->
  [(ChunkNo, WithBlockSize (Entry blk))]
applyStreamTo ci (StreamToInclusive (RealPoint toSlot _toHash)) =
  takeWhile $ \(_, WithBlockSize _ e) ->
    ChunkLayout.slotNoOfBlockOrEBB ci (blockOrEBB e) <= toSlot

tipFromRemote :: forall blk. ConvertRawHash blk => Remote.RemoteTipInfo -> OnDemandTip blk
tipFromRemote tip =
  OnDemandTip
    { odtSlot = SlotNo (Remote.rtiSlot tip)
    , odtHash = fromRawHash (Proxy @blk) (Remote.rtiHashBytes tip)
    , odtBlockNo = BlockNo (Remote.rtiBlockNo tip)
    }
